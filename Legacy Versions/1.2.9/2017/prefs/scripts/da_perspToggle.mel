/*
Persp toggle 1.0

The MIT License (MIT)

Copyright (c) 2016 Davide Alidosi

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*/

global proc da_perspToggle(){

	string $curPanel;
	string $curCam;
	int $orthoValue;
	float $camRot[];
	float $camRotTemp;
	int $intOfTemp;
	float $rotOver;
	float $camRotFixed[2];

	$curPanel = `getPanel -wf`;
	$curCam = `modelPanel -q -cam $curPanel`;
	$orthoValue = `camera -q -o $curCam`;

	if ($orthoValue == 1){
		setAttr ($curCam +".orthographic") 0;
		viewFit;
		inViewMessage -pos topCenter -msg "Perspective View" -fade;
		
	} else {
	
		$camRot = `camera -q -rot $curCam`;

		for ($i=0; $i < 2; $i++){
		
			$camRotTemp = $camRot[$i] / 360;
			$intOfTemp = (int)$camRotTemp;
			$rotOver = 360 * $intOfTemp;
			$camRotFixed[$i] = $camRot[$i] - $rotOver;
		}

		for ($i=0; $i < 2; $i++){
			if ($camRotFixed[$i] < 0)
				$camRotFixed[$i] += 360;
		}

		if ($camRotFixed[0] >= 45 && $camRotFixed[0] < 135){
			viewSet -fit -an 1 -bo;
			inViewMessage -pos topCenter -msg "Bottom position - Orthographic View" -fade;
		}

		else if ($camRotFixed[0] >= 225 && $camRotFixed[0] < 315){
			viewSet -fit -an 1 -t;
			inViewMessage -pos topCenter -msg "Top position - Orthographic View" -fade;
		}

		else if ($camRotFixed[1] < 45){
			viewSet -fit -an 1 -f;
			inViewMessage -pos topCenter -msg "Front position - Orthographic View" -fade;
		}

		else if ($camRotFixed[1] >= 315 ){
			viewSet -fit -an 1 -f;
			inViewMessage -pos topCenter -msg "Front position - Orthographic View" -fade;
		}

		else if ($camRotFixed[1] >= 45 && $camRotFixed[1] < 135)
		{
			viewSet -fit -an 1 -rs;
			inViewMessage -pos topCenter -msg "Right position - Orthographic View" -fade;
		}

		else if ($camRotFixed[1] >= 135 && $camRotFixed[1] < 225)
		{
			viewSet -fit -an 1 -b;
			inViewMessage -pos topCenter -msg "Back position - Orthographic View" -fade;
		}

		else if ($camRotFixed[1] >= 225 && $camRotFixed[1] < 315)
		{
			viewSet -fit -an 1 -ls;
			inViewMessage -pos topCenter -msg "Left position - Orthographic View" -fade;
		}
	}
}