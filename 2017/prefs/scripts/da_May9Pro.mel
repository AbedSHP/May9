// Store May9 Version
$may9Version = "May9 Pro 2.0.7";

// Inizialize DAG Menu Procedure
catchQuiet(dagMenuProc("", ""));

// Inizialize Break Connections
channelBoxCommand -break;

// Source May9 Pro Scripts
source da_scripts.mel;

// Set new scene opened preferences
global proc da_may9Configurations(){
	string $osVer = `about -os`;
	if (`gmatch $osVer "*mac*"` == 1) {
		setAttr "hardwareRenderingGlobals.multiSampleEnable" 1;
	} else {
		setAttr "hardwareRenderingGlobals.lineAAEnable" 1;
		setAttr "hardwareRenderingGlobals.multiSampleEnable" 1;
	}

	modelEditor -e -acx 1 -tsl 1 modelPanel1;
	modelEditor -e -acx 1 -tsl 1 modelPanel2;
	modelEditor -e -acx 1 -tsl 1 modelPanel3;
	modelEditor -e -acx 1 -tsl 1 modelPanel4;

	outlinerEditor  -e -sam 1 -rfv 1 -atr 1 outlinerPanel1;
}
string $may9Configurations = "da_may9Configurations()";
scriptJob -e "NewSceneOpened" $may9Configurations;

// Source May9 Pro Scripts
source da_scripts.mel;

// Initialize Interactive Play button
da_intPlay();

// Preload MMtoKey as Plug-in
loadPlugin MMtoKey;

// Preload Two Bone IK and Spring IK
ik2Bsolver;
ikSpringSolver;

// Preload Crease Set Editor
python( "from maya.app.general import creaseSetEditor");

// Preload Boss Editor
if ( catchQuiet( `python( "import boss.BossEditor as be" )` ) )
	print "Please check Bifrost installation";

// Source User Named Commands, User Run Time Commands and User Prefs only at first May9 Pro run
if (`optionVar -ex May9ActivationCheck` == 0){
    source da_userPrefs.mel;
    optionVar -iv May9ActivationCheck 1;
}

// Load userRuntime
global proc da_userRuntime(){
    source da_userRuntime.mel;
}
catchQuiet (`da_userRuntime`);
